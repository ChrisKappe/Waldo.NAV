OBJECT Codeunit 82110 WaldoNAVPad Text Class
{
  OBJECT-PROPERTIES
  {
    Date=16/09/16;
    Time=10:37:50;
    Modified=Yes;
    Version List=WaldoNAVPad;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WaldoNAVPadTextBuffer@1100084000 : TEMPORARY Record 82110;
      MaxLenght@1100084003 : Integer;
      CurrentObjectNr@1100084001 : Integer;
      CurrentObject@1100084002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Object";
      CurrentText@1100084004 : Text;
      CurrentHTML@1100084005 : Text;
      CurrentTextIsUpdated@1000 : Boolean;

    PROCEDURE Initialize@1100084000(VAR Text@1100084000 : Text;pMaxLength@1100084001 : Integer);
    BEGIN
      SetMaxLenght(pMaxLength);
      CurrentText := Text;
      CurrentHTML := Text;
      ParseText(CurrentText);

      FINDFIRST;
    END;

    PROCEDURE Initialize2@7(VAR Text@1100084000 : Text;VAR HTML@1000 : Text;pMaxLength@1100084001 : Integer);
    BEGIN
      SetMaxLenght(pMaxLength);
      CurrentText := Text;
      CurrentHTML := HTML;
      ParseText(CurrentText);

      FINDFIRST;
    END;

    PROCEDURE LoadTextFromDialog@1100084008(Editable@1100084001 : Boolean;UseHTMLEditor@1100084002 : Boolean);
    VAR
      WaldoNAVPadShowdialogMeth@1100084000 : Codeunit 82112;
    BEGIN
      WaldoNAVPadShowdialogMeth.ShowDialog(CurrentText,CurrentHTML,CurrentTextIsUpdated,Editable,UseHTMLEditor);

      IF Editable THEN BEGIN
        ParseText(CurrentText);

      END;
    END;

    PROCEDURE GetTextIsUpdated@2() : Boolean;
    BEGIN
      EXIT(CurrentTextIsUpdated);
    END;

    PROCEDURE GetText@1100084006() : Text;
    BEGIN
      EXIT(CurrentText);
    END;

    PROCEDURE GetHTML@1() : Text;
    BEGIN
      IF CurrentHTML = '' THEN
        EXIT(CurrentText)
      ELSE
        EXIT(CurrentHTML);
    END;

    PROCEDURE SetMaxLenght@1100084007(pMaxLenght@1100084000 : Integer);
    BEGIN
      MaxLenght := pMaxLenght;
    END;

    PROCEDURE GetMaxLength@1100084009() : Integer;
    BEGIN
      EXIT(MaxLenght);
    END;

    PROCEDURE GetCurrentTextLine@1100084005() : Text;
    BEGIN
      EXIT(WaldoNAVPadTextBuffer.Textline);
    END;

    PROCEDURE FINDFIRST@1100084004() : Boolean;
    BEGIN
      EXIT(WaldoNAVPadTextBuffer.FINDSET);
    END;

    PROCEDURE COUNT@1100084003() : Integer;
    BEGIN
      EXIT(WaldoNAVPadTextBuffer.COUNT);
    END;

    PROCEDURE NEXT@1100084001() : Integer;
    BEGIN
      EXIT(WaldoNAVPadTextBuffer.NEXT);
    END;

    LOCAL PROCEDURE ParseText@1100084002(VAR Text@1100084000 : Text);
    VAR
      WaldoNAVPadTextParseMeth@1100084001 : Codeunit 82111;
    BEGIN
      WaldoNAVPadTextParseMeth.ParseText(Text,GetMaxLength,WaldoNAVPadTextBuffer);
    END;

    BEGIN
    END.
  }
}

