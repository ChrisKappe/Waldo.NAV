OBJECT Codeunit 82101 WaldoNAVPad ShowTexts Meth
{
  OBJECT-PROPERTIES
  {
    Date=06/09/16;
    Time=14:48:02;
    Modified=Yes;
    Version List=WaldoNAVPad;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ShowTexts@1100084000(Editable@1100084000 : Boolean;UseHTMLEditor@1100084001 : Boolean;VAR RecRef@1100084002 : RecordRef;VAR ReturnWaldoNAVPadTextClass@1000 : Codeunit 82110);
    VAR
      Handled@1100084003 : Boolean;
    BEGIN
      OnBeforeShowTexts(RecRef,Handled);
      DoShowTexts(Editable,UseHTMLEditor,RecRef,ReturnWaldoNAVPadTextClass,Handled);
      OnAfterShowTexts(RecRef);
    END;

    LOCAL PROCEDURE DoShowTexts@1100084002(Editable@1100084000 : Boolean;UseHTMLEditor@1100084007 : Boolean;VAR RecRef@1100084001 : RecordRef;VAR ReturnWaldoNAVPadTextClass@1000 : Codeunit 82110;Handled@1100084003 : Boolean);
    VAR
      WaldoNAVPadTextstore@1100084002 : Record 82101;
      TextFromBlob@1100084004 : Text;
      UpdatedText@1100084005 : Text;
    BEGIN
      IF Handled THEN EXIT;

      TextFromBlob := GetTextFromWNPBlob(RecRef);

      LoadTextfromDialog(TextFromBlob,Editable,UseHTMLEditor,ReturnWaldoNAVPadTextClass);
    END;

    LOCAL PROCEDURE GetTextFromWNPBlob@1100084053(VAR RecRef@1100084000 : RecordRef) : Text;
    VAR
      TempBlob@1100084001 : Record 99008535;
      WaldoNavPadBlobstore@1100084003 : Record 82100;
    BEGIN
      WITH WaldoNavPadBlobstore DO BEGIN
        SETRANGE("Record ID", RecRef.RECORDID);
        IF NOT FINDFIRST THEN EXIT('');
        IF NOT Blob.HASVALUE THEN EXIT('');

        CALCFIELDS(Blob);
        TempBlob.Blob := Blob;

        EXIT(GetTextFromBlob(TempBlob));
      END;
    END;

    LOCAL PROCEDURE LoadTextfromDialog@1100084001(VAR TextFromBlob@1100084002 : Text;Editable@1100084003 : Boolean;UseHTMLEditor@1100084004 : Boolean;VAR ResultWaldoNAVPadTextClass@1100084000 : Codeunit 82110);
    VAR
      WaldoNAVPadTextstore@1100084001 : Record 82101;
    BEGIN
      WITH ResultWaldoNAVPadTextClass DO BEGIN
        Initialize(TextFromBlob, MAXSTRLEN(WaldoNAVPadTextstore.Textline));
        LoadTextFromDialog(Editable,UseHTMLEditor);
      END;
    END;

    LOCAL PROCEDURE GetTextFromBlob@1100084047(VAR TempBlob@1100084000 : Record 99008535) : Text;
    VAR
      ReadStream@1100084002 : InStream;
      TextBigText@1100084003 : BigText;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(ReadStream);
      TextBigText.READ(ReadStream);
      EXIT(FORMAT(TextBigText));
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeShowTexts@1100084004(VAR RecRef@1100084002 : RecordRef;VAR Handled@1100084000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterShowTexts@1100084008(VAR RecRef@1100084001 : RecordRef);
    BEGIN
    END;

    BEGIN
    END.
  }
}

