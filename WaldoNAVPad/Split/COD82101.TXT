OBJECT Codeunit 82101 WaldoNAVPad ShowTexts Meth
{
  OBJECT-PROPERTIES
  {
    Date=26/02/16;
    Time=[ 7:49:35];
    Modified=Yes;
    Version List=WaldoNAVPad;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ShowTexts@1100084000(Editable@1100084000 : Boolean;VAR RecRef@1100084002 : RecordRef);
    VAR
      Handled@1100084003 : Boolean;
    BEGIN
      OnBeforeShowTexts(RecRef,Handled);
      DoShowTexts(Editable,RecRef,Handled);
      OnAfterShowTexts(RecRef);
    END;

    LOCAL PROCEDURE DoShowTexts@1100084002(Editable@1100084000 : Boolean;VAR RecRef@1100084001 : RecordRef;Handled@1100084003 : Boolean);
    VAR
      WaldoNAVPadTextstore@1100084002 : Record 82101;
      Text@1100084004 : Text;
      UpdatedText@1100084005 : Text;
    BEGIN
      IF Handled THEN EXIT;

      FilterWNPTexts(WaldoNAVPadTextstore, RecRef);

      Text := GetTextFromWNPBlob(RecRef);
      UpdatedText := ShowDialog(Text);

      UpdateTextForRecord(Text, UpdatedText,MAXSTRLEN(WaldoNAVPadTextstore.Textline),RecRef);
    END;

    LOCAL PROCEDURE GetTextFromWNPBlob@1100084053(VAR RecRef@1100084000 : RecordRef) : Text;
    VAR
      TempBlob@1100084001 : Record 99008535;
      WaldoNavPadBlobstore@1100084003 : Record 82100;
    BEGIN
      WITH WaldoNavPadBlobstore DO BEGIN
        SETRANGE("Record ID", RecRef.RECORDID);
        IF NOT FINDFIRST THEN EXIT('');
        IF NOT Blob.HASVALUE THEN EXIT('');

        CALCFIELDS(Blob);
        TempBlob.Blob := Blob;

        EXIT(GetTextFromBlob(TempBlob));
      END;
    END;

    LOCAL PROCEDURE FilterWNPTexts@1100084005(VAR WaldoNAVPadTextstore@1100084002 : Record 82101;VAR RecRef@1100084001 : RecordRef);
    BEGIN
      WITH WaldoNAVPadTextstore DO BEGIN
        SETRANGE("Record ID", RecRef.RECORDID);
      END;
    END;

    LOCAL PROCEDURE ShowDialog@1100084001(VAR Text@1100084000 : Text) : Text;
    VAR
      WaldoNAVPad@1100084002 : Page 82100;
    BEGIN
      WaldoNAVPad.SetText(Text);
      WaldoNAVPad.LOOKUPMODE := TRUE;
      IF WaldoNAVPad.RUNMODAL = ACTION::LookupCancel THEN
          EXIT(Text);
      EXIT(WaldoNAVPad.GetText);
    END;

    LOCAL PROCEDURE UpdateTextForRecord@1100084012(OldText@1100084000 : Text;NewText@1100084001 : Text;MaxLength@1100084004 : Integer;VAR RecRef@1100084003 : RecordRef);
    BEGIN
      IF OldText = NewText THEN EXIT;

      SaveTextToBlob(NewText,RecRef);;
      SaveTextToRecords(NewText,MaxLength,RecRef);
    END;

    LOCAL PROCEDURE SaveTextToBlob@1100084024(VAR Text@1100084002 : Text;VAR RecRef@1100084000 : RecordRef);
    BEGIN
      DeleteWNPBlobForRecord(RecRef);
      InsertWPNBlobForRecord(Text,RecRef);
    END;

    LOCAL PROCEDURE DeleteWNPBlobForRecord@1100084019(VAR RecRef@1100084000 : RecordRef);
    VAR
      WaldoNavPadBlobstore@1100084001 : Record 82100;
    BEGIN
      WITH WaldoNavPadBlobstore DO BEGIN
        SETRANGE("Record ID", RecRef.RECORDID);
        DELETEALL(FALSE);
      END;
    END;

    LOCAL PROCEDURE InsertWPNBlobForRecord@1100084018(VAR Text@1100084001 : Text;VAR RecRef@1100084000 : RecordRef);
    VAR
      WaldoNavPadBlobstore@1100084004 : Record 82100;
      TempBlob@1100084002 : Record 99008535;
    BEGIN
      WITH WaldoNavPadBlobstore DO BEGIN
        GetBlobFromText(Text,TempBlob);
        INIT;
        "Record ID" := RecRef.RECORDID;
        Blob := TempBlob.Blob;
        TableNo := RecRef.NUMBER;
        INSERT;
      END;
    END;

    LOCAL PROCEDURE SaveTextToRecords@1100084025(VAR Text@1100084002 : Text;MaxLength@1100084001 : Integer;VAR RecRef@1100084000 : RecordRef);
    BEGIN
      DeleteWNPTextForRecord(RecRef);
      InsertWPNTextForRecord(Text,MaxLength,RecRef);
    END;

    LOCAL PROCEDURE DeleteWNPTextForRecord@1100084003(VAR RecRef@1100084000 : RecordRef);
    VAR
      WaldoNAVPadTextstore@1100084001 : Record 82101;
    BEGIN
      FilterWNPTexts(WaldoNAVPadTextstore, RecRef);
      WaldoNAVPadTextstore.DELETEALL(FALSE);
    END;

    LOCAL PROCEDURE InsertWPNTextForRecord@1100084016(VAR Text@1100084001 : Text;MaxLength@1100084003 : Integer;VAR RecRef@1100084000 : RecordRef);
    VAR
      WaldoNAVPadTextClass@1100084002 : Codeunit 82110;
    BEGIN
      WITH WaldoNAVPadTextClass DO BEGIN
        Initialize(Text,MaxLength);

        IF FINDFIRST THEN
          REPEAT
            InsertWPNText(GetCurrentTextLine, RecRef);
          UNTIL NEXT < 1;
      END;
    END;

    LOCAL PROCEDURE InsertWPNText@1100084020(pTextline@1100084000 : Text;VAR RecRef@1100084002 : RecordRef);
    VAR
      WaldoNAVPadTextstore@1100084001 : Record 82101;
    BEGIN
      WITH WaldoNAVPadTextstore DO BEGIN
        INIT;

        Textline := pTextline;
        "Record ID" := RecRef.RECORDID;
        TableNo := RecRef.NUMBER;

        INSERT;
      END;
    END;

    LOCAL PROCEDURE GetBlobFromText@1100084010(Text@1100084001 : Text;VAR TempBlob@1100084000 : Record 99008535);
    VAR
      WriteStream@1100084002 : OutStream;
      TextBigText@1100084003 : BigText;
    BEGIN
      TempBlob.Blob.CREATEOUTSTREAM(WriteStream);
      TextBigText.ADDTEXT(Text);
      TextBigText.WRITE(WriteStream);
    END;

    LOCAL PROCEDURE GetTextFromBlob@1100084047(VAR TempBlob@1100084000 : Record 99008535) : Text;
    VAR
      ReadStream@1100084002 : InStream;
      TextBigText@1100084003 : BigText;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(ReadStream);
      TextBigText.READ(ReadStream);
      EXIT(FORMAT(TextBigText));
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeShowTexts@1100084004(VAR RecRef@1100084002 : RecordRef;VAR Handled@1100084000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterShowTexts@1100084008(VAR RecRef@1100084001 : RecordRef);
    BEGIN
    END;

    BEGIN
    END.
  }
}

